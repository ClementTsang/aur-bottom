# Maintainer: Clement Tsang (xoronth) <cjhtsang@uwaterloo.ca>

pkgname=bottom-bin
pkgver=0.6.3
pkgrel=1
pkgdesc='A cross-platform graphical process/system monitor with a customizable interface and a multitude of features.'
provides=('bottom')
conflicts=('bottom')
arch=('x86_64' 'aarch64' 'armv7h')
url="https://github.com/ClementTsang/bottom"
license=(MIT)

source=(LICENSE::${url}/raw/${pkgver}/LICENSE)
source_x86_64=(archive-${pkgver}.tar.gz::${url}/releases/download/${pkgver}/bottom_x86_64-unknown-linux-gnu.tar.gz)
source_aarch64=(archive-${pkgver}.tar.gz::${url}/releases/download/${pkgver}/bottom_aarch64-unknown-linux-gnu.tar.gz)
source_armv7h=(archive-${pkgver}.tar.gz::${url}/releases/download/${pkgver}/bottom_armv7-unknown-linux-gnueabihf.tar.gz)

sha512sums=(SKIP)
sha512sums_x86_64=('fb6f321845037ee5c90011916a3aa7d83c1d45f4c1b0526888a8a5bfc8f6a1876b1e1c783edfc0aab154fc11de5f162015534cbe9cdb8c8a7c712fbf53a14a86')
sha512sums_aarch64=('ba1c6b2fefcb823c8b1d28d8252ad742f105a35d7c495ea9d6154c63c23f0a0c7354a6162588a279761898241bd9107f3dba74c89358db65cb66ea5368007481')
sha512sums_armv7h=('3a9d0cd97a1e53bbeb7e9638d43abf0d1ced86db34d0044aea662600096fb8430e6cd00c03672e887e68864870e7d4be53d69d6154b18de689f9809f6c628add')

package() {
    install -Dm755 btm "$pkgdir"/usr/bin/btm
    install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE

    install -Dm644 completion/_btm "$pkgdir/usr/share/zsh/site-functions/_btm"
    install -Dm644 completion/btm.bash "$pkgdir/usr/share/bash-completion/completions/btm"
    install -Dm644 completion/btm.fish "$pkgdir/usr/share/fish/vendor_completions.d/btm.fish"
}
